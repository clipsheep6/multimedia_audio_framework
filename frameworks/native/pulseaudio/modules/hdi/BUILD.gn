# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/audio_framework/config.gni")
import("//foundation/multimedia/audio_framework/ressche_part.gni")

pulseaudio_dir = "//third_party/pulseaudio"
pulseaudio_build_path = "//third_party/pulseaudio/ohosbuild"

config("hdi_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "$pulseaudio_dir/include",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir",
    "$pulseaudio_build_path/src",
    "$pulseaudio_build_path/include",
    "${audio_fwk_path}/interfaces/inner_api/native/audiocommon/include",
    "${audio_fwk_path}/frameworks/native/audioschedule/include",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink/common",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink/primary",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink/file",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink/bluetooth",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink/remote",
    "${audio_fwk_path}/frameworks/native/hdiadapter/source/common",
    "${audio_fwk_path}/frameworks/native/hdiadapter/source/primary",
    "${audio_fwk_path}/frameworks/native/hdiadapter/source/file",
    "${audio_fwk_path}/frameworks/native/hdiadapter/source/remote",
    "//drivers/peripheral/audio/interfaces/include",
    "//commonlibrary/c_utils/base/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-DHAVE_CONFIG_H",
    "-D_GNU_SOURCE",
    "-D__INCLUDED_FROM_PULSE_AUDIO",
  ]
}

ohos_shared_library("module-hdi-sink") {
  sanitize = {
    cfi = true
    debug = false
    blocklist = "//foundation/multimedia/audio_framework/cfi_blocklist.txt"
  }
  sources = [
    "hdi_sink.c",
    "module_hdi_sink.c",
  ]

  configs = [ ":hdi_config" ]

  cflags = [ "-DPA_MODULE_NAME=libmodule_hdi_sink_z_so" ]

  ldflags = [
    "-Wl",
    "--no-undefined",
  ]

  deps = [
    "$pulseaudio_build_path/src:pulsecommon",
    "$pulseaudio_build_path/src/pulse:pulse",
    "$pulseaudio_build_path/src/pulsecore:pulsecore",
    "${audio_fwk_path}/frameworks/native/audioschedule:audio_schedule",
    "${audio_fwk_path}/frameworks/native/hdiadapter/sink:renderer_sink_adapter",
  ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  if (ressche_enable == true) {
    external_deps += [ "resource_schedule_service:ressched_client" ]
  }

  subsystem_name = "multimedia"
  part_name = "audio_framework"
}

ohos_shared_library("module-hdi-source") {
  sanitize = {
    cfi = true
    debug = false
    blocklist = "//foundation/multimedia/audio_framework/cfi_blocklist.txt"
  }
  sources = [
    "hdi_source.c",
    "module_hdi_source.c",
  ]

  configs = [ ":hdi_config" ]

  cflags = [ "-DPA_MODULE_NAME=libmodule_hdi_source_z_so" ]

  deps = [
    "$pulseaudio_build_path/src:pulsecommon",
    "$pulseaudio_build_path/src/pulse:pulse",
    "$pulseaudio_build_path/src/pulsecore:pulsecore",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/source:capturer_source_adapter",
    "//third_party/bounds_checking_function:libsec_shared",
  ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  subsystem_name = "multimedia"
  part_name = "audio_framework"
}
